<!DOCTYPE html>
<html>
	<head>
		<meta charset="utf-8">
		<title>Fractals</title>
		<script src="/three.min.js"></script>
		<script src="https://threejs.org/examples/js/controls/OrbitControls.js"></script>
		<script type="text/javascript" src="dat.gui.min.js"></script>
  </head>
	<style>
		body { margin: 0; }
		canvas { display: block; }
	</style>
	</head>
	<body> 
		  <script id="vertexShader" type="x-shader/x-fragment">
            varying vec2 vUv;

            void main()
            {
              vUv = uv;
              vec4 mvPosition = modelViewMatrix * vec4( position, 1.0 );
              gl_Position = projectionMatrix * mvPosition;
            }
         </script>
         <script id="mandelbrotFragmentShader" type="x-shader/x-fragment">
            uniform float time;
            uniform float zoom;
            uniform float orX;
            uniform float orY;
            uniform float scale;
            varying vec2 vUv;
            uniform vec3 colorA;
            uniform vec3 colorB;
            uniform vec3 colorC;
            uniform vec3 colorD;
            uniform float t;

             vec3 bump3 (vec3 x) {
                vec3 y = vec3(1.,1.,1.) - x * x;
                y = max(y, vec3(0.,0.,0.));
                return y;
            }
            vec3 palette( float t) {
                return colorA + colorB*cos( 9.28*(colorC*t+colorD) );
            }


                vec3 spectralGems (float x) {
                    return bump3
                    (   vec3
                        (
                            4. * (x - 0.7), // Red
                            4. * (x - 0.5), // Green
                            4. * (x - 0.23) // Blue
                        )
                    );
                }


                float distanceEstimatorMandelbrot(vec2 c) {
                    vec2 z = vec2(0.);
                    vec2 dz = vec2(0.); 

                    float m2 = 255.;
                    float dis = 1000.;
                    
                
                    for( int i=0; i<1000; i++ )
                    {
                        if( m2 > 1000.0) {  break; }

                        dz = 2.0*vec2(z.x*dz.x-z.y*dz.y, z.x*dz.y + z.y*dz.x) + vec2(1.);

                        z = vec2( z.x*z.x - z.y*z.y, 2.0*z.x*z.y ) + c;
                

                        m2 = dot(z,z);  
                    }
                    return sqrt( m2/dot(dz,dz) )*log(m2) *0.5;
                }



                void main() {
                    vec2 v = vUv;
                    v = v*scale-vec2(orX, orY);

                    v *= pow( 0.7, 5.0 + 13.*(1.+zoom));

                    float f = distanceEstimatorMandelbrot(v);      //f(z) = z ^ 2 + k
                
                    vec3 col;
                    float d = clamp( pow(4.0*f,0.0998),0., 1.);
                    col = spectralGems(d);
    
                    gl_FragColor = vec4(vec3(palette(d)),1.);
                }
         </script>

		  <script id="mandelbulbfragmentShader" type="x-shader/x-fragment">
            uniform float time;
            uniform float camX;
            uniform float camY;
            uniform float camZ;
            uniform float zoom;
            uniform vec3 colorA;
            uniform vec3 colorB;
            uniform float t;
            varying vec2 vUv;
            uniform float power;
            uniform float rotate;
            uniform float light;
            uniform float bailout;
            
            #define PHI (sqrt(5)*0.5 + 0.5)

            //Color functions
            vec3 palette( in float t, in vec3 a, in vec3 b, in vec3 c, in vec3 d )
            {
                return a + b*cos( 9.28*(c*t+d) );
            }

            void pR(inout vec2 p, float a) {
                p = cos(a)*p + sin(a)*vec2(p.y, -p.x);
            }


            //FRACTAl 3D
            float DE3(vec3 pos) {
                vec3 z = pos;
                float dr = 1.0;
                float r = 0.0;
                for (int i = 0; i < 10 ; i++) {
                    r = length(z);
                    if (r>bailout) break;
                    
                    // convert to polar coordinates
                    float theta = acos(z.z/r);
                    float phi = atan(z.y,z.x);
                    dr =  pow( r, power-1.0)*power*dr ;

                    // scale and rotate the point
                    float zr = pow( r,power);
                    theta = theta*power;
                    phi = phi*power;
                    
                    // convert back to cartesian coordinates
                    z = zr*vec3(sin(theta)*cos(phi), sin(phi)*sin(theta), cos(theta));
                    z+=pos;
                }
                return 0.3*log(r)*r/dr;
            }



            float scene(vec3 ray){
            
                ray = ray-vec3(0.03,0.5,1.);
                vec3 ray2 = ray;
                //Spin it - change the plane
                pR(ray2.zx,rotate); // rotating
                
                 return max(DE3(ray2),0.);
            }

            vec3 estimateNormal(vec3 p) {
                float smallNumber = 0.1;
                vec3 n = vec3(
                scene(vec3(p.x + smallNumber, p.yz)) -
                scene(vec3(p.x - smallNumber, p.yz)),
                scene(vec3(p.x, p.y + smallNumber, p.z)) -
                scene(vec3(p.x, p.y - smallNumber, p.z)),
                scene(vec3(p.xy, p.z + smallNumber)) -
                scene(vec3(p.xy, p.z - smallNumber))
            );

            return normalize(n);
            }


            float lighting(vec3 origin, vec3 dir, vec3 normal) {
                vec3 lightPos = vec3(cos(time/20.)*5., sin(time), 1.);
                vec3 lightv = normalize(lightPos - origin);
                
                float diffuse = max(0., dot(lightv, normal));
                vec3 reflectedRay = light* dot(lightv, normal) * normal - lightv;
                
                float specular = max(0.3, (pow(dot(reflectedRay, lightv), 4.)));
                float ambient = 0.08;
                
                return ambient + diffuse + specular;

            }

            vec4 trace(vec3 rayOrigin, vec3 dir){
                vec3 ray = rayOrigin;
                float dist = 0.; 
                float totalDist = 0.;
                float maxDist = 5.;
                
                for (int i = 0; i < 40 ; i++){
                    dist = scene(ray);
                
                    if(dist < 0.0001){
                        vec4 distCol = vec4(1. - vec4(totalDist/maxDist));
                        vec3 hsvCol = palette(
                            distCol.b,
                            colorA/255.,
                            colorB/255.,
                            vec3(.5, 1.0, .8),
                            vec3(.6, .4, .2));
                        vec4 lightingCol = vec4(lighting(rayOrigin,dir,estimateNormal(ray)));
                        return vec4(hsvCol,1.)*t + lightingCol*(1. - t);
                    } 
                    totalDist += dist;
                    ray += dist * dir;
                }   
                return vec4(0.);
            }

            vec3 lookAt(vec2 uv, vec3 camOrigin, vec3 camTarget){
                vec3 zAxis = normalize(camTarget - camOrigin);
                vec3 up = vec3(-0.03,0.4,0.2);
                vec3 xAxis = normalize(cross(up, zAxis));
                vec3 yAxis = normalize(cross(zAxis, xAxis));

                float fov = 2.;

                vec3 dir = (normalize(uv.x * xAxis + uv.y * yAxis + zAxis * fov));

                return dir;
            }


            void main() {
                vec2 v = vUv;
                v = v *4. - 2.;
                v *= pow( 0.7, 5.0 + 13.*(1.+zoom));
            
                vec3 camOrigin = vec3(camX, camY, camZ); //z axis foward and backwards
                vec3 camTarget = vec3(0, 0, 0);
                vec3 rayOrigin = vec3(camOrigin.xy, camOrigin.z - 2.5);  
            
                vec3 direction = lookAt(v, camOrigin, camTarget);
                gl_FragColor = trace(rayOrigin, direction);
            
            }
		</script>
		<script src="/mandelbrot.js"></script>
	</body>
</html>